// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    
    // Register
    Register(in = muxOUT, load = loadREG, out = loop, out = out);


    // Determine if any control signals were set
    Or(a = inc, b = load, out = incORload);
    Or(a = incORload, b = reset, out = loadREG);

    // Combinational logic to implement logic in topmost description
    Inc16(in = loop, out = incremented);
    Mux16(a = loop, b = incremented, sel = inc, out = muxONE);
    Mux16(a = muxONE, b = in, sel = load, out = muxTWO);
    Mux16(a = muxTWO, b = false, sel = reset, out = muxOUT);
}
